<!-- livebook:{"app_settings":{"slug":"gen-nostr"},"autosave_interval_s":30} -->

# GenNostr Example

```elixir
Mix.install([
  {:castore, "~> 0.1"},
  {:gen_nostr, path: "./"},
  {:kino, "~> 0.8.0"},
  {:kino_vega_lite, "~> 0.1.7"},
  {:jason, "~> 1.4"}
])
```

## Introduction

`gen_nostr` is low level Nostr client that uses `mint_web_socket` as the websocket client. `gen_nostr` has a similar  
This is an example how to use it

```elixir
defmodule NostrClient do
  @moduledoc false

  require Logger

  use GenNostr

  def start_link(args) do
    GenNostr.start_link(__MODULE__, args, name: __MODULE__)
  end

  # Callbacks

  @impl GenNostr
  def init(args) do
    relays = Keyword.get(args, :relays, [])
    options = Keyword.get(args, :options, [])

    Enum.each(relays, &GenNostr.add_relay(&1, options))

    {:ok, %{}}
  end

  @impl GenNostr
  def handle_connect(relay, state) do
    Logger.info("Connected to relay: #{inspect(relay.url)}")

    [
      "REQ",
      "1234567890abcdefg",
      %{"kinds" => [1, 6], "since" => 1_678_384_318, "until" => 1_678_387_918, "limit" => 1}
    ]
    |> Jason.encode!()
    |> GenNostr.send_message(relay.url)

    {:ok, state}
  end

  @impl GenNostr
  def handle_disconnect(reason, relay, state) do
    Logger.info("Disconnected from relay: #{inspect(relay.url)} - #{inspect(reason)}")

    case reason do
      {:remove_relay, _} -> Logger.info("Don't reconnect")
      _ -> GenNostr.reconnect(relay)
    end

    {:ok, state}
  end

  @impl GenNostr
  def handle_error(reason, relay, state) do
    Logger.info("Error from relay: #{inspect(relay.url)} - #{inspect(reason)}")
    {:ok, state}
  end

  @impl GenNostr
  def handle_event(message, relay, state) do
    Logger.info("#{message} - #{inspect(relay.url)}")
    {:ok, state}
  end

  @impl GenNostr
  def handle_notice(message, relay, state) do
    Logger.info("#{message} - #{inspect(relay.url)}")
    {:ok, state}
  end

  @impl GenNostr
  def handle_eose(message, relay, state) do
    Logger.info("#{message} - #{inspect(relay.url)}")
    {:ok, state}
  end

  @impl GenNostr
  def handle_ok(message, relay, state) do
    Logger.info("#{message} - #{inspect(relay.url)}")
    {:ok, state}
  end

  @impl GenNostr
  def handle_auth(message, relay, state) do
    Logger.info("#{message} - #{inspect(relay.url)}")
    {:ok, state}
  end
end
```

```elixir
relays = [
  "ws://localhost:7001",
  "ws://localhost:7002",
  "wss://nostrical.com/",
  "wss://relay.orangepill.dev/"
]

NostrClient.start_link(relays: relays)
```

```elixir
Kino.Process.render_sup_tree(GenNostr.PrimarySupervisor)
```

```elixir
GenNostr.list_relays()
```

```elixir
GenNostr.remove_relay("wss://relay.orangepill.dev/")
```

```elixir
subs =
  ["REQ", "0", %{"kinds" => [0], "limit" => 20}]
  |> Jason.encode!()
  |> GenNostr.broadcast()
```
